apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    defaultConfig {
        applicationId "com.kanedias.dybr.fair"
        targetSdkVersion 27 // device you test on
        minSdkVersion 16    // device you allow your users to be on :)
        versionCode 7
        versionName "1.2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${System.getProperty('user.home')}/devel/keystore/Kanedias")
            storePassword "release"
            keyAlias "kanedias"
            keyPassword "release"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "purity"
    productFlavors {
        fdroid {
            dimension "purity"
        }

        googleplay {
            dimension "purity"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"     // Kotlin language support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5"       // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"    // Kotlin UI context
    implementation "com.android.support:design:$supportLibVersion"              // Material design support lib
    implementation "com.android.support:appcompat-v7:$supportLibVersion"        // basic toolbars etc.
    implementation "com.android.support:cardview-v7:$supportLibVersion"         // snappy cardview for lists
    implementation "ch.acra:acra-mail:$acraVersion"                             // crash handler
    implementation "ch.acra:acra-dialog:$acraVersion"
    implementation 'com.jakewharton:butterknife:8.8.1'                          // Annotation processor
    implementation 'com.j256.ormlite:ormlite-android:5.0'                       // ORM for local storage
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'                          // android http client
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0'               // material design for dialogs
    implementation 'com.github.Adonai.convalida-extended:convalida:1.1.0'       // validation of text inputs
    implementation 'moe.banana:moshi-jsonapi:3.3.0'                             // JSON-API parser
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'                      // Moshi Kotlin support
    implementation 'com.squareup.moshi:moshi-adapters:1.5.0'                    // Moshi serializers collection
    implementation 'ru.noties:markwon:1.0.4'                                    // markdown rendering
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'           // show gif in posts
    //implementation 'com.github.clans:fab:1.6.4'                               // Floating action button with menu

    googleplayImplementation 'com.android.billingclient:billing:1.0'            // billing library from Google

    kapt 'com.jakewharton:butterknife-compiler:8.8.1'                           // Kotlin annotation processors
    kapt 'com.github.Adonai.convalida-extended:convalida-compiler:1.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.0'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'org.awaitility:awaitility:3.0.0'                        // useful wait-for-condition
    androidTestImplementation 'org.apache.commons:commons-text:1.2'                    // random string generator
}
kotlin {
    experimental {
        coroutines "enable"
    }
}
